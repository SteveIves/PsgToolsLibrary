;****************************************************************************
;
; Title:		WinFileCopy.dbl
;
; Author:		Steve Ives (Synergex Professional Services Group)
;
; Date:			10th June 1999
;
; Description:	Copy a file
;
; Platforms:	32-bit Microsoft Windows platforms only.
;
;****************************************************************************
;
; This code is supplied as seen and without warranty or support, and is used
; at your own risk. Neither the author or Synergex accept any responsability
; for any loss or damage which may result from the use of this code.
;
;****************************************************************************
;

.function WinFileCopy, ^VAL

;Argument list

	a_from		,a		;Source file name
	a_to		,a		;Target file name
	a_over		,n		;Overwrite if file exists> (TRUE = YES)
	a_confirm	,n		;Confirm actions (TRUE=YES)

;End of argument list

.include "WND:windows.def"

.align
stack record
	status		,i4	
	dll     	,i4
	from		,a256
	to			,a256
	over		,i4

.proc

	status = 0

	xcall w_proc(WP_HOURGLASS_ON)

	dll = %dll_open("kernel32.dll")

	xcall fill(%char(0),from)
	from(1:%trim(a_from)) = a_from

	xcall fill(%char(0),to)
	to(1:%trim(a_to)) = a_to

	over=1			;Don't overwrite existing files
	if (^passed(a_over).and.a_over)
		clear over	;Overwrite existing file

	if (.not.%dll_call(dll,DLL_TYPE_WINAPI,"CopyFileA",^addr(from),^addr(to),over))
		status = 1

	if (^passed(a_confirm).and.a_confirm)
	begin
		if (status) then
			xcall info("Failed to copy " + %atrim(a_from) + " to " + %atrim(a_to) + "!")
		else
			xcall info("Copied " + %atrim(a_from) + " to " + %atrim(a_to))
	end

	xcall dll_close(dll)
	
	xcall w_proc(WP_HOURGLASS_OFF)

	freturn (status)

.end

